1 KEY OFF
11 DIM MAP%(32,19),BALL%(134),R$(20),CURSOR%(134),RI$(20)
20 DEF FN CH(X,Y)=MAP%(X,Y)*128+MAP%(X+1,Y)*64+MAP%(X+2,Y)*32+MAP%(X+3,Y)*16+MAP%(X+4,Y)*8+MAP%(X+5,Y)*4+MAP%(X+6,Y)*2+MAP%(X+7,Y)
98 LA$=CHR$(0)+CHR$(75):RA$=CHR$(0)+CHR$(77):UA$=CHR$(0)+CHR$(72):DA$=CHR$(0)+CHR$(80):EK$="q"
200 SCREEN 1
300 OPEN"r",#2,"b:screens",128
400 FIELD #2,9 AS B$,4 AS R$(0),4 AS R$(1),4 AS R$(2),4 AS R$(3),4 AS R$(4),4 AS R$(5),4 AS R$(6),4 AS R$(7),4 AS R$(8),4 AS R$(9),4 AS R$(10),4 AS R$(11),4 AS R$(12),4 AS R$(13),4 AS R$(14),4 AS R$(15),4 AS R$(16),4 AS R$(17),4 AS R$(18),4 AS R$(19)
500 CLOSE #2
600 KEY 10,"goto 900"+CHR$(13)
700 CLS:CIRCLE(100,100),4,1:PAINT(100,100),1,1:GET(100-4,100-4)-(100+4,100+4),BALL%:CLS
710 CLS:DRAW"bm100,100d5r5u5l5":GET(100,100)-(105,105),CURSOR%:CLS
800 GOSUB 9000
805 X=0:Y=0:BX=0:BY=0:R$=" "
810 GOSUB 2200:IF A$="" THEN PUT(X*10+2,Y*10+2),CURSOR%,XOR:PUT(X*10+2,Y*10+2),CURSOR%,XOR:GOTO 810
820 DX=0:DY=0:IF A$=UA$ THEN DY=-1 ELSE IF A$=DA$ THEN DY=1 ELSE IF A$=LA$ THEN DX=-1 ELSE IF A$=RA$ THEN DX=1
830 X=X+DX:Y=Y+DY:IF X<0 THEN X=0 ELSE IF X>31 THEN X=31
832 IF Y<0 THEN Y=0 ELSE IF Y>19 THEN Y=19
840 IF A$=" " THEN GOSUB 8000
850 IF A$="c" THEN GOSUB 9000
855 IF A$="g" THEN LOCATE 20,1:INPUT "get map ",A:GOSUB 6700
860 IF A$="p" THEN LOCATE 20,1:INPUT "put map ",A:GOSUB 10000
865 IF A$="b" THEN LOCATE 1,1:INPUT"ball coords ",BX,BY:PUT(BX*10,BY*10),BALL%,XOR
866 IF A$="r" THEN IF R$="y" THEN R$=" " ELSE R$="y"
890 GOTO 810
900 SCREEN 2:SCREEN 0,0,0
1000 END
1100 '
1200 ' init screen
1300 '
1400 CLS
1500 B=192:FOR A=0 TO (5+3*L-C)*7 STEP 7:LINE(A,B)-(A+5,B+5),2,B:LINE(A+1,B+1)-(A+4,B+4),2,B:NEXT
1600 LINE(0,9)-(319,190),1,B
1700 LOCATE 1,1: PRINT"SCORE:000000":LOCATE 1,20:PRINT"HI-SCORE:000000":FOR WA=300 TO 290+10*(MEN-1) STEP 10:DRAW"bm"+STR$(WA)+",-1c1d2l2u2r2ld4nl3nr3d2ng2nf2":PRESET(WA,0):PRESET(WA-2,0):PRESET(WA-1,0):NEXT
1800 LOCATE 1,14-LEN(STR$(SC)):PRINT MID$(STR$(SC),2)
1900 LOCATE 1,36-LEN(STR$(HS)):PRINT MID$(STR$(HS),2)
2000 FOR I=1 TO 31:FOR J=1 TO 18:MAP%(I,J)=0:NEXT J,I
2197 '
2198 ' inkey replacement
2199 '
2200 A$=INKEY$
2210 IF LEN(A$)=2 THEN LB=ASC(LEFT$(A$,1)):RB=ASC(RIGHT$(A$,1))
2230 IF A$=EK$ THEN GOSUB 900:END
2280 RETURN
2397 '
2398 ' clear buffer
2399 '
2400 DEF SEG=0: POKE 1050, PEEK(1052)
2500 RETURN
2600 '
2700 ' ball
2800 '
2900 PUT(BX,BY),BALL%,XOR
3000 BX=BX+BDX*S:BY=BY+BDY*S
3100 IF BX<5 THEN BDX=-BDX ELSE IF BX>290 THEN BDX=-BDX
3200 IF BY<10 THEN BDY=-BDY ELSE IF BY>175 THEN BDY=-BDY
3300 IF MAP%(BX/10,BY/10)=2 THEN 3800
3400 IF MAP%(ABS(BX-S)/10,BY/10)<>0 THEN BDX=-BDX:GOTO 3800
3500 IF MAP%((BX+S)/10,BY/10)<>0 THEN BDX=-BDX:GOTO 3800
3600 IF MAP%(BX/10,ABS(BY-S)/10)<>0 THEN BDY=-BDY:GOTO 3800
3700 IF MAP%(BX/10,(BY+S)/10)<>0 THEN BDY=-BDY
3800 IF BX<0 THEN BX=0 ELSE IF BX>319 THEN BX=319
3900 IF BY<10 THEN BY=10 ELSE IF BY>190 THEN BY=190
4000 PUT(BX,BY),BALL%,XOR
4100 RETURN
4800 '
4900 ' draw block at (x,y)
5000 '
5100 LINE(X*10+1,Y*10)-(X*10+9,Y*10+8),1,BF:LINE(X*10,Y*10+4)-(X*10+9,Y*10+4),0:LINE(X*10+3,Y*10)-(X*10+3,Y*10+4),0:LINE(X*10+6,Y*10+4)-(X*10+6,Y*10+9),0
5200 RETURN
5300 '
5400 ' draw screen
5500 '
5600 CLS: FOR Y=0 TO 19
5700 FOR X=0 TO 31
6000 IF MAP%(X,Y)=1 THEN GOSUB 5100
6100 NEXT X
6200 NEXT Y
6300 RETURN
6400 '
6500 ' get screen a, draw screen
6600 '
6700 OPEN"r",#2,"b:screens",128
6800 FIELD #2,9 AS B$,4 AS R$(0),4 AS R$(1),4 AS R$(2),4 AS R$(3),4 AS R$(4),4 AS R$(5),4 AS R$(6),4 AS R$(7),4 AS R$(8),4 AS R$(9),4 AS R$(10),4 AS R$(11),4 AS R$(12),4 AS R$(13),4 AS R$(14),4 AS R$(15),4 AS R$(16),4 AS R$(17),4 AS R$(18),4 AS R$(19)
6900 GET #2,A
6910 FOR Y=0 TO 19
6920 BIT=7
6930 FOR X=0 TO 31
6940 IF X/8=INT(X/8) THEN A=ASC(MID$(R$(Y),INT(X/8)+1,1))
6950 IF A-2^BIT<0 THEN MAP%(X,Y)=0 ELSE MAP%(X,Y)=1:A=A-2^BIT
6960 BIT=BIT-1:IF BIT<0 THEN BIT=7
6970 NEXT X,Y:R$=MID$(B$,9,1):BX=CVS(LEFT$(B$,4)):BY=CVS(MID$(B$,5,4))
7000 CLOSE #2:GOSUB 5600
7200 X=0:Y=0:RETURN
7300 SCREEN 2:SCREEN 0,0,0
7400 '
7402 ' flip block
7403 '
8000 IF MAP%(X,Y)=0 THEN MAP%(X,Y)=1 ELSE MAP%(X,Y)=0
8030 IF MAP%(X,Y)=1 THEN GOSUB 4900 ELSE LINE(X*10,Y*10)-(X*10+9,Y*10+9),0,BF
8040 RETURN
8997 '
8998 ' clear screen
8999 '
9000 FOR I=0 TO 31:FOR J=0 TO 19:MAP%(I,J)=0:NEXT J,I:CLS
9005 FOR I=0 TO 200 STEP 10:LINE(0,I)-(340,I),1:NEXT
9006 FOR I=0 TO 320 STEP 10:LINE(I,0)-(I,200),1:NEXT
9010 RETURN
9997 '
9998 ' put map on disk
9999 '
10000 OPEN"r",#2,"b:screens",128
10010 FIELD #2,9 AS B$,4 AS R$(0),4 AS R$(1),4 AS R$(2),4 AS R$(3),4 AS R$(4),4 AS R$(5),4 AS R$(6),4 AS R$(7),4 AS R$(8),4 AS R$(9),4 AS R$(10),4 AS R$(11),4 AS R$(12),4 AS R$(13),4 AS R$(14),4 AS R$(15),4 AS R$(16),4 AS R$(17),4 AS R$(18),4 AS R$(19)
10014 RSET B$=MKS$(BX)+MKS$(BY)+R$
10015 FOR Y=0 TO 19
10016 C$=CHR$(FN CH(0,Y))+CHR$(FN CH(8,Y))+CHR$(FN CH(16,Y))+CHR$(FN CH(24,Y))
10017 RSET R$(Y)=C$
10018 NEXT Y
10020 PUT #2,A
10030 CLOSE #2
10040 GOSUB 6700
10050 RETURN

