10 RANDOMIZE TIMER
20 DEF FN RAND(X)=INT(RND(1)*X+1)
30 OPEN "r",#1,"b:scores.dat",14
40 FIELD 1,10 AS DAT$,4 AS SCORE$
50 GET #1,1:HS=CVS(SCORE$):CLOSE
60 KEY 10,"gosub 610"+CHR$(13)
63 '
64 'key mod
65 LA$=CHR$(0)+CHR$(75):RA$=CHR$(0)+CHR$(77):UA$=CHR$(0)+CHR$(72):DA$=CHR$(0)+CHR$(80):EK$="q"
70 DIM MAP%(32,19),BALL%(134),TAIL%(500,1),DIR%(4,1),SKULL%(134),BOX%(134),L$(9),R$(19),BRICK%(134)
80 KEY OFF
90 DB=100:GOSUB 2400
100 SCREEN 1:DRAW"bm100,100r4d1r1d1r1d3l1d1l1d2l1dl2u1l1u2l1u1l1u3r1u1r1":PAINT(101,101),1,3:DRAW"c0bm99,103rd":DRAW"bm+3,0ur":DRAW"bm-1,5l2":GET(98,100)-(107,109),SKULL%
110 CLS:CIRCLE(100,100),4,1:PAINT(100,100),1,1:GET(100-4,100-4)-(100+4,100+4),BALL%:CLS:' :FOR X=1 TO 10000:NEXT X
120 LINE(100,100)-(109,108),1,BF:LINE(100,104)-(109,104),0:LINE(103,100)-(103,104),0:LINE(106,104)-(106,109),0:GET(100,100)-(109,109),BRICK%:CLS
130 RESTORE
140 V=1:L=1:SC=0:C=0:D!=DD!*.12
150 FOR MEN = 3 TO 1 STEP -1
160 FL=1:DX=0:DY=0:X=160:Y=95:DE=100:OX=X:OY=Y:BX=102:BY=50:BDX=1:BDY=1:BA=0:HEAD=1:TAIL=0:BD=0:INDEX=4
170 SCREEN 1,0,0
180 GOSUB 1310 'clear screen
190 TAIL%(0,0)=OX:TAIL%(0,1)=OY
200 DIR(0,0)=0:DIR(1,0)=1:DIR(2,0)=0:DIR(3,0)=-1:DIR(4,0)=0
210 DIR(0,1)=1:DIR(1,1)=0:DIR(2,1)=-1:DIR(3,1)=0:DIR(4,1)=0
215 CR$="programed by TONY JACQUES"
220 '
230 ' main keyboard routine
240 '
250 GOSUB 1541
252 'IF TIMER - R < .01 THEN 252
256 IF A$="" THEN CIRCLE (X,Y),2,3: CIRCLE (X,Y),2,0:GOTO 250
260 IF NOT(A$=RA$ OR A$=LA$ OR A$=UA$ OR A$=DA$ OR A$=" " OR A$="q" OR A$="s") THEN 250
270 IF A$=" " THEN FL=0
280 IF FL=1 THEN 320
282 IF A$="q" THEN GOSUB 610:END
290 IF A$=AR$ THEN INDEX=INDEX-1 ELSE IF A$=AD$ THEN INDEX=INDEX+1
300 IF INDEX<0 THEN INDEX=3 ELSE IF INDEX>3 THEN INDEX=0
310 GOTO 330
320 IF A$=LA$ THEN INDEX=3 ELSE IF A$=RA$ THEN INDEX=1 ELSE IF A$=UA$ THEN INDEX=2 ELSE IF A$=DA$ OR A$=DA$ THEN INDEX=0
330 DX=DIR(INDEX,0):DY=DIR(INDEX,1)
340 IF A$="m" THEN 1520
350 FOR I=1 TO D!:NEXT I:X=X+DIR(INDEX,0)*3:Y=Y+DIR(INDEX,1)*3:IF A$="s" THEN PRINT "D!="+STR$(D!)+" "; :INPUT D!:GOTO 130
360 IF X<0 THEN X=0 ELSE IF X>319 THEN X=319
370 IF Y<10 THEN Y=10 ELSE IF Y>190 THEN Y=190
380 IF POINT(X,Y)=1 OR POINT(X,Y)=3 THEN 650
390 IF POINT(X,Y)<>0 OR MAP%(INT(X/10),INT(Y/10))=1 THEN C=C+1:GOSUB 880
400 IF BD<>0 THEN BD=BD-1:IF BD=0 THEN LINE(BNSX+2,BNSY+2)-(BNSX+7,BNSY+7),0,BF
410 LINE(OX,OY)-(X,Y):IF MAP%(INT(X/10),INT(Y/10))=0 THEN MAP%(INT(X/10),INT(Y/10))=3
420 GOSUB 1460:OX=X:OY=Y
430 IF Q<>0 THEN LOCATE 10,17:Q=Q-1:PRINT Q;:GOSUB 980:GOSUB 1580 ELSE 450
440 IF Q=1 THEN Q=0:GOTO 520 ELSE 480
450 IF FNRAND(20)=1 THEN GOSUB 980
460 GOSUB 1580 'erase tail
470 IF C>5+L*3 THEN 520
480 GOSUB 1541
482 GOTO 280
490 '
500 ' next level
510 '
520 C=0:D!=D!-100
530 LOCATE 12,16:PRINT"LEVEL:"L:LOCATE 13,16:PRINT"BONUS:"L*100:FOR I=1 TO DD!*4:NEXT I:SC=SC+100*L
540 L=L+1:IF L/4<>INT(L/4) THEN V=V+1 ELSE Q=400
550 GOSUB 1410 'clear buffer
560 GOTO 160
580 '
590 ' text mode
600 '
610 SCREEN 2:SCREEN 0,0,0:RETURN
620 '
630 ' dead man
640 '
650 LINE(OX,OY)-(X,Y),1:GOSUB 1460
660 GOSUB 1240
670 IF TAIL<>HEAD-SGN(HEAD-TAIL) THEN 660
680 IF SC>HS THEN HS=SC
690 IF Q<>0 THEN Q=0:L=L+1:C=0
700 NEXT MEN:LOCATE 12,16:PRINT"GAME OVER"
710 GOSUB 1410
720 X=1:OPEN "r",#1,"b:scores.dat",14
730 FIELD 1,10 AS DAT$,4 AS SCORE$
740 IF X=11 THEN 780 ELSE GET #1,X:IF CVS(SCORE$)>SC THEN X=X+1:GOTO 740
750 IF X=10 THEN 770
760 FOR Y=10 TO X+1 STEP -1:GET #1,Y-1:PUT #1,Y:NEXT Y
770 GOSUB 1410:LOCATE 20,1:GOSUB 800:LSET DAT$=NA$:RSET SCORE$=MKS$(SC):PUT #1,X
780 CLOSE:GOSUB 1710:GOSUB 1410 'clear buffer
790 SCREEN 1:GOTO 130
800 CLS:INPUT "NAME:";NA$:IF NA$="" THEN 800
810 FOR I=1 TO LEN(NA$):A=ASC(MID$(NA$,I,1)):IF A>96 AND A<123 THEN A=A-32
820 IF A<65  OR A> 90 THEN 800 ELSE MID$(NA$,I)=CHR$(A):NEXT I
830 IF NA$="TONY" THEN NA$="CREATOR"
840 RETURN
850 '
860 ' erase block, score
870 '
880 A=INT(X/10)*10:B=INT(Y/10)*10:IF MAP%(A/10,B/10)=4 THEN SC=SC+90:BD=0:LINE(A+2,B+2)-(A+7,B+7),0,BF:GOTO 900
890 PUT(A,B),BOX%,XOR:MAP%(A/10,B/10)=0
900 MAP%(A/10,B/10)=0
910 SC=SC+10:LOCATE 1,14-LEN(STR$(SC)):PRINT MID$(STR$(SC),2)
920 DE=DE+10
930 B=192:LINE ((6+3*L-C)*7,B)-((5+3*L-C+9)*7,B+9),0,BF
940 RETURN
950 '
960 ' put object on the screen
970 '
980 IF BA=1 THEN PUT(BX,BY),BALL%,XOR
990 A=FNRAND(30)*10:B=FNRAND(16)*10
1000 IF X<(A+40) AND X>(A-40) AND Y<(B+40) AND Y>(B-40) THEN 990
1010 ON MAP%(A/10,B/10)+1 GOSUB 1060,1120,1020,1020,1020:IF BA=1 THEN PUT(BX,BY),BALL%,XOR
1020 RETURN
1030 '
1040 ' put block on screen
1050 '
1060 IF SC>100 AND RND(1)>.8 THEN 1180
1070 IF RND(1)>.9 AND BD=0 THEN LINE(A+2,B+2)-(A+7,B+7),2,BF:MAP%(INT(A/10),INT(B/10))=4:BNSX=A:BNSY=B:BD=DB ELSE PUT(A,B),BOX%,XOR:MAP%(A/10,B/10)=1
1080 RETURN
1090 '
1100 ' erase block
1110 '
1120 MAP%(INT(A/10),INT(B/10))=0
1130 PUT(A,B),BOX%,XOR
1140 RETURN
1150 '
1160 ' put skull on screen
1170 '
1180 PUT (A+1,B),SKULL%,XOR:'DRAW"bm"+STR$(A)+","+STR$(B)+"c0d5c1r8d1l4u5r1d8l1u5":
1190 MAP%(INT(A/10),INT(B/10))=2:RETURN
1200 '
1210 ' erase end of snake
1220 '
1230 IF DE>1 THEN DE=DE-1:RETURN
1240 IF TAIL=500 THEN T=0 ELSE T=TAIL+1
1250 LINE(TAIL%(TAIL,0),TAIL%(TAIL,1))-(TAIL%(T,0),TAIL%(T,1)),0:TAIL=TAIL+1:IF TAIL>500 THEN TAIL=0
1260 IF NOT(INT(X/10)=INT(TAIL%(T,0)) AND INT(Y/10)=INT(TAIL%(T,1))) THEN MAP%(INT(TAIL%(T,0)/10),INT(TAIL%(T,1)/10))=0
1270 RETURN
1280 '
1290 ' init screen
1300 '
1310 CLS:GOSUB 1820:IF Q=0 THEN GOSUB 2020:GOSUB 1940 ELSE FOR I=0 TO 31:FOR J=0 TO 19:MAP%(I,J)=0:NEXT J,I
1320 B=192:FOR A=0 TO (5+3*L-C)*7 STEP 7:LINE(A,B)-(A+5,B+5),2,B:LINE(A+1,B+1)-(A+4,B+4),2,B:NEXT
1330 LINE(0,9)-(319,190),1,B
1340 LOCATE 1,1: PRINT"SCORE:000000":LOCATE 1,20:PRINT"HI-SCORE:000000":FOR WA=300 TO 290+10*(MEN-1) STEP 10:DRAW"bm"+STR$(WA)+",-1c1d2l2u2r2ld4nl3nr3d2ng2nf2":PRESET(WA,0):PRESET(WA-2,0):PRESET(WA-1,0):NEXT
1350 LOCATE 1,14-LEN(STR$(SC)):PRINT MID$(STR$(SC),2)
1360 LOCATE 1,36-LEN(STR$(HS)):PRINT MID$(STR$(HS),2)
1370 'FOR I=1 TO 31:FOR J=1 TO 18:MAP%(I,J)=0:NEXT J,I:GOSUB 1600
1380 '
1390 ' clear keyboard buffer
1400 '
1410 DEF SEG=0: POKE 1050, PEEK(1052)
1420 RETURN
1430 '
1440 ' put head in queue
1450 '
1460 TAIL%(HEAD,0)=X:TAIL%(HEAD,1)=Y
1470 HEAD=HEAD+1:IF HEAD>500 THEN HEAD=0
1480 RETURN
1490 '
1500 ' print map
1510 '
1520 FOR A=10 TO 320 STEP 10:FOR B=10 TO 180 STEP 10:ON MAP%(A/10,B/10) GOSUB 1070,1180,1180
1530 NEXT B,A
1531 GOSUB 1410
1540 GOSUB 1541:IF A$="" THEN 1540 ELSE RUN
1541 A$=INKEY$
1542 IF A$="q" THEN GOSUB 610:END
1543 RETURN
1550 '
1560 ' ball
1570 '
1580 IF BA=0 THEN 1230 ELSE PUT(BX,BY),BALL%,XOR:S=3:GOSUB 1230
1590 BX=BX+BDX*S:BY=BY+BDY*S
1600 IF BX<5 THEN BDX=-BDX ELSE IF BX>290 THEN BDX=-BDX
1610 IF BY<10 THEN BDY=-BDY ELSE IF BY>175 THEN BDY=-BDY
1620 IF MAP%(BX/10,BY/10)=2 THEN 1670
1630 IF MAP%(ABS(BX-S)/10,BY/10)<>0 THEN BDX=-BDX:GOTO 1670
1640 IF MAP%((BX+S)/10,BY/10)<>0 THEN BDX=-BDX:GOTO 1670
1650 IF MAP%(BX/10,ABS(BY-S)/10)<>0 THEN BDY=-BDY:GOTO 1670
1660 IF MAP%(BX/10,(BY+S)/10)<>0 THEN BDY=-BDY
1670 IF BX<0 THEN BX=0 ELSE IF BX>319 THEN BX=319
1680 IF BY<10 THEN BY=10 ELSE IF BY>190 THEN BY=190
1690 PUT(BX,BY),BALL%,XOR
1700 RETURN
1710 '
1720 ' print scores
1730 '
1740 OPEN "r",#1,"b:scores.dat",14:CLS:PRINT:PRINT TAB(14)"HIGH SCORES":PRINT
1750 FOR X=1 TO 10:GET #1,X
1760 PRINT TAB(13) DAT$;" " CVS(SCORE$)
1765 NEXT X
1770 CLOSE #1:PRINT:PRINT:PRINT"YOUR SCORE:";SC:FOR X=1 TO DD!*4:GOSUB 1541:IF A$="" THEN NEXT X
1780 RETURN
1790 '
1800 ' get box% from disk
1810 '
1820 OPEN "r",#2,"b:shapes.shp",128
1830 FIELD #2,10 AS L$(0),10 AS L$(1),10 AS L$(2),10 AS L$(3),10 AS L$(4),10 AS L$(5),10 AS L$(6),10 AS L$(7),10 AS L$(8),10 AS L$(9)
1840 IF V>20 THEN V=V-20:GOTO 1840
1850 GET #2,V:CLOSE #2
1860 FOR I=0 TO 9:FOR J=0 TO 8
1870 IF MID$(L$(J),I+1,1)="1" THEN PSET(I+100,J),2 ELSE PRESET(I+100,J)
1880 NEXT J,I
1890 GET(100,0)-(109,9),BOX%
1900 RETURN
1910 '
1920 ' draw screen
1930 '
1940 RETURN:FOR YY=1 TO 18
1950 FOR XX=0 TO 31
1960 IF MAP%(XX,YY)=2 THEN PUT(XX*10,YY*10),BRICK%,XOR
1970 NEXT XX,YY
1980 RETURN
1990 '
2000 ' get screen l, draw screen
2010 '
2020 OPEN"r",#2,"b:screens",128
2030 FIELD #2,9 AS B$,4 AS R$(0),4 AS R$(1),4 AS R$(2),4 AS R$(3),4 AS R$(4),4 AS R$(5),4 AS R$(6),4 AS R$(7),4 AS R$(8),4 AS R$(9),4 AS R$(10),4 AS R$(11),4 AS R$(12),4 AS R$(13),4 AS R$(14),4 AS R$(15),4 AS R$(16),4 AS R$(17),4 AS R$(18),4 AS R$(19)
2040 IF V>20 THEN V=V-20:GOTO 2040
2050 GET #2,V
2060 BX=CVS(MID$(B$,1,4))*10:BY=CVS(MID$(B$,5,4))*10:IF BX=0 AND BY=0 THEN BA=0 ELSE BA=1:PUT(BX,BY),BALL%,XOR:MAP%(BX/10,BY/10)=2
2070 R$=RIGHT$(B$,1)
2080 BIT=7:FOR YY=1 TO 18
2090 FOR XX=0 TO 31
2100 IF BIT=7 THEN A=ASC(MID$(R$(YY),INT(XX/8)+1,1))
2110 IF A-2^BIT<0 THEN MAP%(XX,YY)=0 ELSE MAP%(XX,YY)=2:A=A-2^BIT:PUT(XX*10,YY*10),BRICK%,XOR
2120 BIT=BIT-1:IF BIT<0 THEN BIT=7
2130 NEXT XX,YY
2140 IF R$<>"y" THEN 2170 ELSE FOR I=1 TO 3*L
2150 XX=FN RAND(31):YY=FN RAND(18):IF MAP%(XX,YY)<>0 THEN 2150
2160 MAP%(XX,YY)=2:PUT(XX*10,YY*10),BRICK%,XOR:NEXT
2170 CLOSE #2
2180 RETURN
2197 '
2198 ' inkey delay
2199 '
2200 A$=INKEY$
2210 IF LEN(A$)=2 THEN LB=ASC(LEFT$(A$,1)):RB=ASC(RIGHT$(A$,1))
2230 IF A$="q" THEN GOSUB 610:END
2280 RETURN
2297 '
2298 ' Delay 4 seconds OR keypress
2299 '
2300 R=TIMER:GOSUB 1410
2310 WHILE (TIMER <= R+4)
2315 GOSUB 2200:IF A$<>"" THEN 2330
2320 WEND
2330 RETURN
2397 '
2398 ' Set timing variables
2399 ' D! is the difficulty setting, DD! is the 1 second delay constant
2400 D!=0:R=TIMER
2410 FOR X=1 TO D!:NEXT X
2420 IF TIMER-R<.25 THEN D!=D!+100:R=TIMER:GOTO 2410
2425 DD!= D!*4
2490 RETURN
